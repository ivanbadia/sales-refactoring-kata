group 'SalesTax'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'groovy'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

sourceSets {
    integrationTest {
        groovy.srcDir "$projectDir/src/integration-test/groovy"
        resources.srcDir "$projectDir/src/integration-test/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest

ext {
    jGivenVersion = '0.18.2'
    junitVersion = '5.3.1'
}

dependencies {
    implementation 'io.vavr:vavr:0.10.3'

    testImplementation 'org.assertj:assertj-core:3.16.1'
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"

    integrationTestImplementation "org.codehaus.groovy:groovy-all:2.5.12"
    integrationTestImplementation "com.tngtech.jgiven:jgiven-core:$jGivenVersion"
    integrationTestImplementation "com.tngtech.jgiven:jgiven-html5-report:$jGivenVersion"
    integrationTestImplementation "com.tngtech.jgiven:jgiven-spock:$jGivenVersion"
}

compileGroovy {
    dependsOn tasks.getByPath('compileJava')
    classpath += files(compileJava.destinationDir)
}

compileTestGroovy {
    dependsOn tasks.getByPath('compileTestJava')
    classpath += files(compileTestJava.destinationDir)
}
